buildscript {
  ext {
    springBootVersion = '1.3.3.RELEASE'
    assertjVersion = '3.3.0'
    cucumberVersion = '1.2.4'
    seleniumVersion = '2.53.0'
    htmlunitVersion = '2.52.0'
  }

  repositories {
    mavenLocal()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'net.researchgate:gradle-release:2.3.4'
    classpath 'org.ajoberstar:gradle-git:1.4.2'
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2"
    classpath 'se.transmode.gradle:gradle-docker:1.2'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'ls-shipfast'

jar {
  version = '0.0.1'
}

distDocker {
  exposePort 8080
}

docker {
  baseImage = 'anapsix/alpine-java:8'
}

bootRepackage  {
  classifier = 'exec'
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/intTest/java')
    }
    resources.srcDir file('src/intTest/resources')
  }
  acceptanceTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/acceptTest/java')
    }
    resources.srcDir file('src/acceptTest/resources')
  }
}

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime

  acceptanceTestCompile.extendsFrom testCompile
  acceptanceTestRuntime.extendsFrom testRuntime
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }
  compile "org.springframework.boot:spring-boot-starter-jetty"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-devtools"
  compile "org.springframework.boot:spring-boot-starter-thymeleaf"

  testCompile "junit:junit"
  testCompile "org.assertj:assertj-core:$assertjVersion"
  testCompile "org.springframework.boot:spring-boot-starter-test"

  testCompile "info.cukes:cucumber-java:$cucumberVersion"
  testCompile "info.cukes:cucumber-junit:$cucumberVersion"
  testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
  testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:$htmlunitVersion"
}

tasks.withType(JavaCompile) {
  options.incremental = true
}

tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")
}

task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

task acceptanceTest(type: Test) {
  testClassesDir = sourceSets.acceptanceTest.output.classesDir
  classpath = sourceSets.acceptanceTest.runtimeClasspath
  outputs.upToDateWhen { false }

  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.acceptanceTestRuntime +
              sourceSets.main.output +
              sourceSets.acceptanceTest.output
      args = [
              '--plugin', 'pretty',
              '--glue', 'com.shipfast.test.acceptance.steps', 'src/acceptTest/resources'
      ]
    }
  }
}

task startAcceptanceServices(type: Exec, group: 'docker') {
  // Define acceptance test services
  commandLine 'echo', 'TBD'
}

task stopAcceptanceServices(group: 'docker') << {
  // Clean-up acceptance test services
}

task startIntegrationServices(type: Exec, group: 'docker') {
  // Define integration services
  commandLine 'docker', 'run', '-p', '27017', '--name', 'db', '-d', 'mongo'
}
task stopIntegrationServices(group: 'docker') << {
  // Clean-up integration services
  exec { commandLine 'docker', 'stop', 'db' }
  exec {
    ignoreExitValue true
    commandLine 'docker', 'rm', 'db'
  }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
integrationTest.dependsOn startIntegrationServices
integrationTest.finalizedBy stopIntegrationServices

check.dependsOn acceptanceTest
acceptanceTest.mustRunAfter integrationTest
acceptanceTest.dependsOn startAcceptanceServices
acceptanceTest.finalizedBy stopAcceptanceServices

task createWrapper(type: Wrapper) {
  gradleVersion = '2.9'
}
